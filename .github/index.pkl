amends "@pkl.impl.ghactions/PklCI.pkl"

import "@com.github.actions/catalog.pkl"
import "@com.github.actions/Workflow.pkl"

triggerDocsBuild = "release"

local function baseJob(_cache: ("maven"|"gradle"|"sbt")?): Workflow.Job = new {
  `runs-on` = "ubuntu-latest"
  steps {
    (catalog.`actions/checkout@v6`) {
      with {
        // full checkout (needed for spotless)
        `fetch-depth` = 0
      }
    }
    (catalog.`actions/setup-java@v5`) {
      with {
        `java-version` = "17"
        distribution = "temurin"
        cache = _cache
      }
    }
    new {
      name = "Run tests"
      // TODO: figure out why this needs to be run twice.
      // Without this, completion tests fail (for example, see https://app.circleci.com/pipelines/github/apple/pkl-intellij/126/workflows/7de1355e-6b5a-4502-b71f-30d6ae42900e/jobs/245/tests)
      // language=bash
      run =
        """
        # revert GHA's default `set -e` to prevent process exit from the first failure
        set +e
        ./gradlew check || ./gradlew check
        """
    }
    (catalog.`actions/upload-artifact@v5`) {
      name = "Upload test report HTML"
      `if` = "!cancelled()"
      with {
        name = "test-reports-html"
        path = "build/reports/tests/**/*"
      }
    }
  }
}

testReports {
  junit {
    "build/test-results/**/*.xml"
  }
}

local buildAndTest: Workflow = new {
  jobs {
    ["build-and-test"] = (baseJob("gradle")) {
      name = "Build and test"
    }
  }
}

build = buildAndTest

prb = buildAndTest

main = buildAndTest

releaseBranch = buildAndTest

release {
  jobs {
    ["publish"] = (baseJob(null)) {
      name = "Release"
      permissions {
        contents = "write"
      }
      steps {
        new {
          name = "Run gradle publish"
          run = "./gradlew -DreleaseBuild=true publish"
        }
        new {
          name = "Create GitHub release"
          env {
            ["GH_TOKEN"] = "${{ github.token }}"
            ["TAG_NAME"] = "${{ github.ref_name }}"
          }
          // language=bash
          run =
            #"""
            echo "Preparing updatePlugins.xml"
            sed -i -e 's/#version#/'${TAG_NAME}'/g' release/updatePlugins.xml
            cp build/m2/org/pkl/pkl-intellij/*/*.zip release

            echo "Creating release"
            gh release create ${TAG_NAME} \
              --title "${TAG_NAME}" \
              --target "${{ github.sha }}" \
              --verify-tag \
              --notes "Release notes: https://pkl-lang.org/intellij/current/changelog.html#release-${TAG_NAME}" \
              --repo "${{ github.repository }}" \
              release/*.*
            """#
        }
      }
    }
  }
}
