amends "@gha/Workflow.pkl"

import "../shared/base.pkl"

name = "Release"

on {
  push {
    `branches-ignore` {
      "*"
    }
    tags {
      "*"
    }
  }
}

jobs {
  ["publish"] = (base.baseJob) {
    name = "Release"
    permissions {
      contents = "write"
    }
    steps {
      new {
        name = "Run gradle publish"
        run = "./gradlew -DreleaseBuild=true publish"
      }
      new {
        name = "Create GitHub release"
        env {
          ["GH_TOKEN"] = "${{ github.token }}"
        }
        // language=bash
        run = #"""
          echo "Preparing updatePlugins.xml"
          TAG_NAME=${{ github.ref_name }}
          sed -i -e 's/#version#/'${TAG_NAME}'/g' release/updatePlugins.xml

          echo "Creating release"
          gh release create ${TAG_NAME} \
            --title "${TAG_NAME}" \
            --target "${{ github.sha }}" \
            --verify-tag \
            --notes "Release notes: https://pkl-lang.org/intellij/current/changelog.html#release-${TAG_NAME}" \
            --repo "${{ github.repository }}" \
            release/*.*
          """#
      }
    }
  }
}
