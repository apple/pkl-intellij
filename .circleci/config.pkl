amends "package://pkg.pkl-lang.org/pkl-project-commons/pkl.impl.circleci@1.1.1#/PklCI.pkl"

prb = buildWorkflow

main = buildWorkflow

triggerDocsBuild = "release"

release {
  jobs {
    "build-publish"
    new {
      ["do-release"] {
        requires = new Listing { "build-publish" }
        context = "pkl-github-release"
      }
    }
  }
}

local baseJob: Job = new {
  docker {
    new {
      image = "cimg/openjdk:17.0"
    }
  }
  steps {
    "checkout"
  }
}

jobs {
  ["build"] = (baseJob) {
    steps {
      new RunStep {
        // TODO: figure out why this needs to be run twice.
        // Without this, completion tests fail (for example, see https://app.circleci.com/pipelines/github/apple/pkl-intellij/126/workflows/7de1355e-6b5a-4502-b71f-30d6ae42900e/jobs/245/tests)
        command = #"""
          ./gradlew --info --stacktrace -DtestReportsDir=${HOME}/test-results build \
            || ./gradlew --info --stacktrace -DtestReportsDir=${HOME}/test-results build
        """#
      }
      new StoreTestResults { path = "~/test-results" }
    }
  }
  ["build-publish"] = (baseJob) {
    steps {
      new RunStep {
        // TODO: figure out why this needs to be run twice.
        command = #"""
            ./gradlew --info --stacktrace -DtestReportsDir=${HOME}/test-results build \
              || ./gradlew --info --stacktrace -DtestReportsDir=${HOME}/test-results build
          """#
      }
      new RunStep {
        command = "./gradlew --info --stacktrace -DtestReportsDir=${HOME}/test-results publish"
      }
      new StoreTestResults { path = "~/test-results" }
      new PersistToWorkspaceStep {
        root = "."
        paths {
          "release/*.xml"
          "build/m2/org/pkl/pkl-intellij/*/*.zip"
        }
      }
    }
  }
  ["do-release"] {
    docker {
      new { image = "maniator/gh:v2.40.1" }
    }
    steps {
      new AttachWorkspaceStep {
        at = "."
      }
      new RunStep {
        name = "gh release"
        command = #"""
          echo "Preparing updatePlugins.xml"
          sed -i -e 's/#version#/'${CIRCLE_TAG}'/g' release/updatePlugins.xml
          cp build/m2/org/pkl/pkl-intellij/*/*.zip release

          echo "Creating release"
          gh release create ${CIRCLE_TAG} \
            --title "${CIRCLE_TAG}" \
            --target "${CIRCLE_SHA1}" \
            --verify-tag \
            --notes "Release notes: https://pkl-lang.org/intellij/current/changelog.html#release-${CIRCLE_TAG}" \
            --repo "${CIRCLE_PROJECT_USERNAME}/${CIRCLE_PROJECT_REPONAME}" \
            release/*.*
          """#
      }
    }
  }
}

local buildWorkflow = new Workflow {
  jobs {
    "build"
  }
}
